buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies { classpath 'com.blackducksoftware.integration:common-gradle-plugin:0.0.+' }
}

plugins {
    id 'org.jenkins-ci.jpi' version '0.25.0'
}

version = '1.4.2-SNAPSHOT'

apply plugin: 'com.blackducksoftware.integration.solution'

group = 'com.blackducksoftware.integration'
description = 'Black Duck Detect Plugin for Jenkins'

task listrepos {
    doLast {
        println "Repositories:"
        project.repositories.each { println "Name: " + it.name + "; url: " + it.url }
    }
}

def jcenterRepo = project.repositories.findByName('BintrayJCenter')
project.repositories.remove(jcenterRepo)
def mavenRepo = project.repositories.findByName('maven')
project.repositories.remove(mavenRepo)

project.afterEvaluate {
    // MavenLocal is added by the gradle-jpi-plugin and the common-gradle-plugin so we have to remove it after the plugins are evaluated
    List<String> repoNames = new ArrayList()
    repoNames.addAll(project.repositories.getNames())
    for (String repoName : repoNames) {
        if (repoName.contains('MavenLocal')) {
            def repoToRemove = project.repositories.findByName(repoName)
            project.repositories.remove(repoToRemove)
        }
    }
}

jenkinsPlugin {
    coreVersion = '2.60.1'
    displayName = 'Black Duck Detect'
    url = 'https://wiki.jenkins.io/display/JENKINS/Black+Duck+Detect+Plugin'
    gitHubUrl = 'https://github.com/jenkinsci/blackduck-detect-plugin'
}

dependencies {
    compile 'com.blackducksoftware.integration:hub-common:29.1.1'
    compile 'org.jvnet.localizer:localizer:1.24'

    jenkinsPlugins 'org.jenkins-ci.plugins:credentials:1.9.4'
    jenkinsPlugins 'org.jenkins-ci.plugins:plain-credentials:1.0'

    optionalJenkinsPlugins 'org.jenkins-ci.plugins:job-dsl:1.67'
    optionalJenkinsPlugins 'org.jenkins-ci.plugins.workflow:workflow-job:2.9'
    optionalJenkinsPlugins 'org.jenkins-ci.plugins.workflow:workflow-cps:2.23'
    optionalJenkinsPlugins 'org.jenkins-ci.plugins.workflow:workflow-step-api:2.5'
}

task renameFile << {
    if (project.getVersion().toString().contains("SNAPSHOT")) {
        File libsDirectory = new File(project.getBuildDir(), "libs")
        File originalFile = new File(libsDirectory, "${project.getName()}.hpi")
        File newFile = new File(libsDirectory, "${project.getName()}-${project.getVersion()}.hpi")
        originalFile.renameTo newFile
    }
}

jpi.finalizedBy renameFile
