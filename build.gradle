import com.synopsys.integration.jenkins.detect.GenerateHtml
import com.synopsys.integration.jenkins.detect.GenerateJelly

buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies { classpath 'com.blackducksoftware.integration:common-gradle-plugin:0.0.+' }
}

plugins {
    id 'org.jenkins-ci.jpi' version '0.31.0'
}

group = 'com.blackducksoftware.integration'
version = '2.0.2-SNAPSHOT'
description = 'Synopsys Detect Plugin for Jenkins'

apply plugin: 'com.blackducksoftware.integration.solution'

def jcenterRepo = project.repositories.findByName('BintrayJCenter')
project.repositories.remove(jcenterRepo)
def mavenRepo = project.repositories.findByName('maven')
project.repositories.remove(mavenRepo)

project.afterEvaluate {
    // MavenLocal is added by the gradle-jpi-plugin and the common-gradle-plugin so we have to remove it after the plugins are evaluated
    List<String> repoNames = new ArrayList()
    repoNames.addAll(project.repositories.getNames())
    for (String repoName : repoNames) {
        if (repoName.contains('MavenLocal')) {
            def repoToRemove = project.repositories.findByName(repoName)
            project.repositories.remove(repoToRemove)
        }
    }
}

jenkinsPlugin {
    coreVersion = '2.60.1'
    displayName = 'Synopsys Detect'
    compatibleSinceVersion = '2.0.0'
    url = 'https://wiki.jenkins.io/display/JENKINS/Synopsys+Detect+Plugin'
    gitHubUrl = 'https://github.com/jenkinsci/synopsys-detect-plugin'

    pluginFirstClassLoader = true
}

dependencies {
    compile 'com.blackducksoftware.integration:blackduck-common:42.1.0'
    compile 'com.synopsys.integration:polaris-common:0.7.3'
    compile 'org.jvnet.localizer:localizer:1.24'

    jenkinsPlugins 'org.jenkins-ci.plugins:credentials:2.1.10'
    jenkinsPlugins 'org.jenkins-ci.plugins:plain-credentials:1.0'

    optionalJenkinsPlugins 'org.jenkins-ci.plugins:job-dsl:1.67'
    optionalJenkinsPlugins 'org.jenkins-ci.plugins.workflow:workflow-job:2.9'
    optionalJenkinsPlugins 'org.jenkins-ci.plugins.workflow:workflow-cps:2.23'
    optionalJenkinsPlugins 'org.jenkins-ci.plugins.workflow:workflow-step-api:2.10'
}

task renameFile {
    doLast {
        if (project.version.toString().contains("SNAPSHOT")) {
            File libsDirectory = new File(project.buildDir, "libs")
            File originalFile = new File(libsDirectory, "${project.getName()}.hpi")
            File newFile = new File(libsDirectory, "${project.getName()}-${project.version}.hpi")
            originalFile.renameTo newFile
        }
    }
}

task generateJelly(type: GenerateJelly) {
    pathToExtensionResourcesPackage = 'src/main/resources/com/synopsys/integration/jenkins/detect/extensions/'
    pathToJellyJson = 'src/main/resources/templates/detect/jelly/'
    outputPathToJson = ['postbuild/DetectPostBuildStep': 'postBuild.json',
                        'global/DetectGlobalConfig'    : 'globalConfig.json',]
}

task generateHelp(type: GenerateHtml) {
    pathToExtensionResourcesPackage = 'src/main/resources/com/synopsys/integration/jenkins/detect/extensions/'
    pathToHelpJson = 'src/main/resources/templates/detect/help/'
    outputPathToJson = ['postbuild/DetectPostBuildStep': 'postBuild.json',
                        'global/DetectGlobalConfig'    : 'globalConfig.json',]
}

compileJava.dependsOn generateHelp, generateJelly
jpi.finalizedBy renameFile
